// Top-level build file where you can add configuration options common to all sub-projects/modules.
subprojects { project ->
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    checkstyle {
        toolVersion = '6.12.1'
    }

    checkstyle {
        configFile = rootProject.file('./config/checkstyle.xml')
        configProperties.checkStyleConfigDir = rootProject.rootDir
    }

    task('checkstyle', type: Checkstyle) {
        source 'src'
        ignoreFailures false
        showViolations true
        include '**/*.java'
        exclude '**/gen/**'

        classpath = files()
    }

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
        }
    }

    task pmd(type: Pmd) {
        ignoreFailures = false
        ruleSetFiles = files("${project.rootDir}/config/pmd-ruleset.xml")
        ruleSets = []

        source 'src'
        include '**/*.java'
        exclude '**/gen/**'

        reports {
            xml.enabled = false
            html.enabled = true
            xml {
                destination file("$project.buildDir/reports/pmd/pmd.xml")
            }
            html {
                destination file("$project.buildDir/reports/pmd/pmd.html")
            }
        }
    }
}

buildscript {

    repositories {
        google()
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        maven { url 'https://maven.aliyun.com/nexus/content/repositories/jcenter' }
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://maven.aliyun.com/repository/central' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        maven { url 'https://maven.aliyun.com/nexus/content/repositories/jcenter' }
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task check(type: GradleBuild) {
    tasks = ['test', 'pmd', 'checkstyle']
}

task publish(type: GradleBuild) {
    tasks = ['clean', 'bintrayUpload']
}
